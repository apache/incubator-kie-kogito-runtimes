/**
 * Copyright 2021 Red Hat, Inc. and/or its affiliates.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.kie.kogito.codegen.unit;
unit AdultUnit;

import org.kie.kogito.codegen.data.Person
import org.kie.kogito.codegen.data.Results

rule FilterAdult when
    $p: /persons[ age >= 18 ]
then
    // In theory it should also work something like
    //    $p.setAdult(true);
    //    persons.update($p);
    // but for now we don't want to enable an update of an object directly on the DataStore from a consequence
    // otherwise all the property reactivity information will be lost. The following modify will anyway have the
    // effect to propagate the update to the DataStore and then to all the rule units sharing it.
    modify($p) { setAdult(true) };
end

rule RemoveNonAdult when
    $p: /persons[ !adult ]
then
    // Invoking remove on a DataStore has the same effect of just calling
    //    delete($p);
    // In both cases the object is removed from the DataStore from where it has been inserted
    // and the deletion will be propagated to all rule units sharing this DataStore.
    persons.remove($p);
end

rule IsAdult when
    $p: /persons[ $name : name ]
then
    results.add($name);
end

