@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand

pipeline {
    agent {
        label 'kie-rhel7 && kie-mem16g'
    }
    tools {
        maven 'kie-maven-3.6.2'
        jdk 'kie-jdk11'
    }
    triggers {
        cron ('H 2 * * *')
    }
    options {
        buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '', numToKeepStr: '5')
        timeout(time: 360, unit: 'MINUTES')
    }
    parameters {
        string(name: 'BRANCH_NAME', defaultValue: "master", description: 'Set the branch to build&test')
    }
    environment {
        KOGITO_CI_EMAIL_TO = credentials('KOGITO_CI_EMAIL_TO')
        MAVEN_OPTS = '-Xms1024m -Xmx4g'

        // Set into 'Initialize' stage
        // DROOLS_VERSION
    }
    stages {
        stage("Initialize"){
            steps {
                script {
                    checkoutDroolsRepo()

                    checkoutRepo('kogito-runtimes')
                    checkoutRepo('kogito-runtimes', 'integration-tests')
                    checkoutOptaplannerRepo()
                    checkoutRepo('kogito-apps')
                    checkoutRepo('kogito-examples')
                    checkoutRepo('kogito-examples', 'kogito-examples-persistence')
                    checkoutRepo('kogito-examples', 'kogito-examples-events')
                }
            }
        }
        stage('Retrieve drools snapshot version') {
            steps {
                script {
                    dir('drools') {
                        // query mvn to get the latest version
                        env.DROOLS_VERSION = """${sh (
                                script: 'mvn org.apache.maven.plugins:maven-help-plugin:3.2.0:evaluate -Dexpression=project.version -q -DforceStdout',
                                returnStdout: true
                            ).trim()}"""
                        assert env.DROOLS_VERSION != ""
                        echo "Latest kie7 version is: ${env.DROOLS_VERSION}"
                    }
                }
            }
        }
        stage('Build kogito-runtimes') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('kogito-runtimes')
                        .run('clean install')
                }
            }
        }
        stage('Check Runtimes integration-tests with persistence') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('integration-tests')
                        .withProfiles(['persistence'])
                        .run('clean verify')
                }
            }
        }
        stage('Build optaplanner') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('optaplanner')
                        .run('clean install')
                }
            }
        }
        stage('Build kogito-apps') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('kogito-apps')
                        .run('clean install')
                }
            }
        }
        stage('Build kogito-examples') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('kogito-examples')
                        .run('clean install')
                }
            }
        }
        stage('Build kogito-examples with persistence') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('kogito-examples-persistence')
                        .withProfiles(['persistence'])
                        .run('clean verify')
                }
            }
        }
        stage('Build kogito-examples with events') {
            steps {
                script {
                    getMavenCommandWithDroolsVersion('kogito-examples-events')
                        .withProfiles(['events'])
                        .run('clean verify')
                }
            }
        }
    }
    post {
        unsuccessful {
            sendNotification()
        }
        always {
            junit '**/target/surefire-reports/**/*.xml, **/target/failsafe-reports/**/*.xml'
            cleanWs()
        }
    }
}

void sendNotification(){
    emailext body: "Kogito daily Drools #${BUILD_NUMBER} was: ${currentBuild.currentResult}\nPlease look here: ${BUILD_URL}",
             subject: "[${params.BRANCH_NAME}][d] Runtimes Drools snapshot",
             to: env.KOGITO_CI_EMAIL_TO
}

void checkoutRepo(String repoName, String dirName=repoName) {
    dir(dirName) {
        checkout(githubscm.resolveRepository(repoName, 'kiegroup', params.BRANCH_NAME, false))
    }
}

void checkoutDroolsRepo() {
    dir('drools') {
        checkout(githubscm.resolveRepository('drools', 'kiegroup', 'master', false))
    }
}

void checkoutOptaplannerRepo() {
    String targetBranch = params.BRANCH_NAME
    String [] versionSplit = targetBranch.split("\\.")
    if(versionSplit.length == 3 
        && versionSplit[0].isNumber()
        && versionSplit[1].isNumber()
       && versionSplit[2] == 'x') {
        targetBranch = "${Integer.parseInt(versionSplit[0]) + 7}.${versionSplit[1]}.x"
    } else {
        echo "Cannot parse branch as release branch so going further with current value: ${targetBranch}"
    }
    dir('optaplanner') {
        checkout(githubscm.resolveRepository('optaplanner', 'kiegroup', targetBranch, false))
    }
}

MavenCommand getMavenCommandWithDroolsVersion(String directory){
    return new MavenCommand(this, ['-fae'])
                .withSettingsXmlId('kogito_release_settings')
                .inDirectory(directory)
                .withProperty('version.org.kie7', "${DROOLS_VERSION}")
}
